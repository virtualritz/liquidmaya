//
//
// The contents of this file are subject to the Mozilla Public License Version 1.1 (the
// "License"); you may not use this file except in compliance with the License. You may
// obtain a copy of the License at http://www.mozilla.org/MPL/
//
// Software distributed under the License is distributed on an "AS IS" basis, WITHOUT
// WARRANTY OF ANY KIND, either express or implied. See the License for the specific
// language governing rights and limitations under the License.
//
// The Original Code is the Liquid Rendering Toolkit.
//
// The Initial Developer of the Original Code is Colin Doncaster. Portions created by
// Colin Doncaster are Copyright (C) 2002. All Rights Reserved.
//
// Contributor(s): Philippe Leprince.
//
//
// The RenderMan (R) Interface Procedures and Protocol are:
// Copyright 1988, 1989, Pixar
// All Rights Reserved
//
//
// RenderMan (R) is a registered trademark of Pixar
//
//  Creation Date:  December 7th, 2005
//
//
//  Description:
//      Creates the Liquid Tab in the Unified Render Globals Window.
//
//  Input Arguments:
//
//  Returns:
//


global proc liquidCreateMayaGlobalsTabContent( string $prefix )
{
  string $theTabLayout = ( $prefix + "liquidUnifiedRenderGlobalsMainTabLayout" );

  if ( `tabLayout -q -ex $theTabLayout` ) {

    string $selected[] = `tabLayout -q -tabLabel $theTabLayout`;
    int $tab = `tabLayout -q -selectTabIndex $theTabLayout` - 1;

    if ( $selected[ $tab ] != "" ) {

      string $layout = `tabLayout -q -selectTab $theTabLayout`;
      if ( !size( `layout -q -ca $layout` ) ) eval("liquidGlobals"+$selected[ $tab ]+"Tab_buildIt( \""+$prefix+"\" );");

    }
  }
}


global proc liquidCreateMayaGlobalsTab( string $prefix, string $name )
{
  // sanity check : we need valid globals
  if ( !liquidGlobalsExists() ) liquidCreateGlobals();

  string $theTabLayout = ( $prefix + "liquidUnifiedRenderGlobalsMainTabLayout" );

  setParent $theTabLayout;
  evalEcho ("liquidGlobals"+$name+"Tab( \""+$prefix+"\");");
  string $newlayout = ($prefix+"liquidGlobals"+$name+"Set");
  if ( `layout -q -ex $newlayout` ) tabLayout -e -tabLabel $newlayout $name $theTabLayout;

}





global proc liquidCreateGlobalsTab()
{
  string $currentParent = `setParent -q`;

  if ( !liquidGlobalsExists() ) liquidCreateGlobals();

  // find out if we are going to display the masterLayer's globals or regular globals
  string $prefix = "";
  if ( gmatch( $currentParent, "unifiedRenderGlobalsWindow|rgMainForm|tabForm|masterLayerTabLayout*") ) $prefix = "masterLayer_";

  // make sure the maya globals are correctly initialized.
  if ( !`layout -q -ex commonTabColumn` ) {
    string $parent = `setParent -q`;
    setCurrentRenderer mayaSoftware;
    addRendererTabsToGlobalsWindow mayaSoftware;
    setCurrentRenderer liquid;
    setParent $parent;
  }

  // push the correct templates to create widgets
  setUITemplate -pushTemplate renderGlobalsTemplate;
  setUITemplate -pushTemplate attributeEditorTemplate;

  // add the presets
  menuBarLayout liquidGlobalsMenuBar;
    menu -label "Presets" -helpMenu true -p liquidGlobalsMenuBar liquidGlobalsPresetMenu;
      menuItem -label "Save..." -c liquidSavePreset -p liquidGlobalsPresetMenu liquidGlobalsPresetMenuSave;
      menuItem -label "Save as Default" -enable false -p liquidGlobalsPresetMenu ;
      menuItem -label "Reset to Default" -enable false -p liquidGlobalsPresetMenu ;
      menuItem -divider true -p liquidGlobalsPresetMenu ;
      liquidAddPresets();

  // create the main tab layout
  columnLayout -adjustableColumn true liquidTabColumn;

  separator -style "none" -h 10;

  // build the content of the globals
  string $theTabLayout = ( $prefix + "liquidUnifiedRenderGlobalsMainTabLayout" );

  string $cmd = ("tabLayout -e -w (`window -q -w unifiedRenderGlobalsWindow`) -h (`window -q -h unifiedRenderGlobalsWindow`-170) "+$theTabLayout+";");

  tabLayout -scrollable true -minChildWidth 200
            -cc $cmd
            -sc $cmd
            -dcc $cmd
            $theTabLayout;

  if ( `layout -q -ex liquidTabLayout` ) layout -e -vis false liquidTabLayout;

  liquidCreateMayaGlobalsTab( $prefix, "Frame" );
  liquidCreateMayaGlobalsTab( $prefix, "File" );
  liquidCreateMayaGlobalsTab( $prefix, "Image" );
  liquidCreateMayaGlobalsTab( $prefix, "Displays" );
  liquidCreateMayaGlobalsTab( $prefix, "Rays" );
  liquidCreateMayaGlobalsTab( $prefix, "Options" );
  liquidCreateMayaGlobalsTab( $prefix, "Job" );
  liquidCreateMayaGlobalsTab( $prefix, "Renderer" );
  liquidCreateMayaGlobalsTab( $prefix, "Fluid" );


  // set up the tab so that it may resize and auto-build.
  if ( `layout -q -ex liquidTabLayout` ) tabLayout -e -cc $cmd -sc $cmd -dcc $cmd liquidTabLayout;
  tabLayout -e -psc ("liquidCreateMayaGlobalsTabContent( \""+$prefix+"\" );") $theTabLayout;

  if ( `layout -q -ex liquidTabLayout` ) layout -e -vis true liquidTabLayout;

  evalEcho $cmd;
  evalDeferred -lp $cmd;

  setUITemplate -popTemplate;
  setUITemplate -popTemplate;
}


global proc liquidUpdateMayaGlobals()
{
  if ( `layout -q -ex liquidCommonTab` ) layout -e -m false -vis false liquidCommonTab;

  float $resx = `getAttr liquidGlobals.xResolution`;
  float $resy = `getAttr liquidGlobals.yResolution`;
  setAttr defaultResolution.width $resx;
  setAttr defaultResolution.height $resy;
  setAttr defaultResolution.deviceAspectRatio ($resx/$resy);
}


