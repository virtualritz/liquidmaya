//
//
// The contents of this file are subject to the Mozilla Public License Version 1.1 (the
// "License"); you may not use this file except in compliance with the License. You may
// obtain a copy of the License at http://www.mozilla.org/MPL/
//
// Software distributed under the License is distributed on an "AS IS" basis, WITHOUT
// WARRANTY OF ANY KIND, either express or implied. See the License for the specific
// language governing rights and limitations under the License.
//
// The Original Code is the Liquid Rendering Toolkit.
//
// The Initial Developer of the Original Code is Colin Doncaster. Portions created by
// Colin Doncaster are Copyright (C) 2002. All Rights Reserved.
//
// Contributor(s): Philippe Leprince.
//
//
// The RenderMan (R) Interface Procedures and Protocol are:
// Copyright 1988, 1989, Pixar
// All Rights Reserved
//
//
// RenderMan (R) is a registered trademark of Pixar
//
//  Creation Date:  December 7th, 2005
//
//
//  Description:
//      Creates the Liquid Tab in the Unified Render Globals Window.
//
//  Input Arguments:
//
//  Returns:
//


global proc liquidCreateMayaGlobalsTabContent()
{
  if ( `tabLayout -q -ex liquidUnifiedRenderGlobalsMainTabLayout` ) {

    string $selected[] = `tabLayout -q -tabLabel liquidUnifiedRenderGlobalsMainTabLayout`;
    int $tab = `tabLayout -q -selectTabIndex liquidUnifiedRenderGlobalsMainTabLayout` - 1;

    if ( $selected[ $tab ] != "" ) {

      string $layout = `tabLayout -q -selectTab liquidUnifiedRenderGlobalsMainTabLayout`;
      if ( !size( `layout -q -ca $layout` ) ) eval("liquidGlobals"+$selected[ $tab ]+"Tab_buildIt");

    }
  }
}


global proc liquidCreateMayaGlobalsTab( string $name )
{
  if ( !liquidGlobalsExists() ) liquidCreateGlobals();

  setParent liquidUnifiedRenderGlobalsMainTabLayout;
  eval ("liquidGlobals"+$name+"Tab");
  string $newlayout = ("liquidGlobals"+$name+"Set");
  if ( `layout -q -ex $newlayout` ) tabLayout -e -tabLabel $newlayout $name liquidUnifiedRenderGlobalsMainTabLayout;

}





global proc liquidCreateGlobalsTab()
{
  if ( !liquidGlobalsExists() ) liquidCreateGlobals();

  // make sure the maya globals are correctly initialized.
  if ( !`layout -q -ex commonTabColumn` ) {
    string $parent = `setParent -q`;
    setCurrentRenderer mayaSoftware;
    addRendererTabsToGlobalsWindow mayaSoftware;
    setCurrentRenderer liquid;
    setParent $parent;
  }

  setUITemplate -pushTemplate renderGlobalsTemplate;
  setUITemplate -pushTemplate attributeEditorTemplate;

  // add the presets
  menuBarLayout liquidGlobalsMenuBar;
    menu -label "Presets" -helpMenu true -p liquidGlobalsMenuBar liquidGlobalsPresetMenu;
      menuItem -label "Save..." -c liquidSavePreset -p liquidGlobalsPresetMenu liquidGlobalsPresetMenuSave;
      menuItem -label "Save as Default" -enable false -p liquidGlobalsPresetMenu ;
      menuItem -label "Reset to Default" -enable false -p liquidGlobalsPresetMenu ;
      menuItem -divider true -p liquidGlobalsPresetMenu ;
      liquidAddPresets();

  // create the main tab layout
  columnLayout -adjustableColumn true liquidTabColumn;

  separator -style "none" -h 10;

  string $cmd = ("tabLayout -e -w (`window -q -w unifiedRenderGlobalsWindow`) -h (`window -q -h unifiedRenderGlobalsWindow`-170) liquidUnifiedRenderGlobalsMainTabLayout;");

  tabLayout -scrollable true -minChildWidth 200
            -cc $cmd
            -sc $cmd
            -dcc $cmd
            liquidUnifiedRenderGlobalsMainTabLayout;

  if ( `layout -q -ex liquidTabLayout` ) layout -e -m true liquidTabLayout;

  liquidCreateMayaGlobalsTab( "Frame" );
  liquidCreateMayaGlobalsTab( "File" );
  liquidCreateMayaGlobalsTab( "Image" );
  liquidCreateMayaGlobalsTab( "Displays" );
  liquidCreateMayaGlobalsTab( "Rays" );
  liquidCreateMayaGlobalsTab( "Options" );
  liquidCreateMayaGlobalsTab( "Job" );
  liquidCreateMayaGlobalsTab( "Renderer" );
  liquidCreateMayaGlobalsTab( "Fluid" );


  if ( `layout -q -ex liquidTabLayout` ) tabLayout -e -cc $cmd -sc $cmd -dcc $cmd liquidTabLayout;
  tabLayout -e -psc ("liquidCreateMayaGlobalsTabContent();") liquidUnifiedRenderGlobalsMainTabLayout;

  eval $cmd;
  evalDeferred -lp $cmd;

  setUITemplate -popTemplate;
  setUITemplate -popTemplate;
}


global proc liquidUpdateMayaGlobals()
{
  if ( `layout -q -ex liquidCommonTab` ) layout -e -m false -vis false liquidCommonTab;

  float $resx = `getAttr liquidGlobals.xResolution`;
  float $resy = `getAttr liquidGlobals.yResolution`;
  setAttr defaultResolution.width $resx;
  setAttr defaultResolution.height $resy;
  setAttr defaultResolution.deviceAspectRatio ($resx/$resy);
}


